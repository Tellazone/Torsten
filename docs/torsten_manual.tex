% Created 2018-03-27 Tue 15:59
% Intended LaTeX compiler: pdflatex
\documentclass[11pt, reqno]{amsbook}
              \newcommand\subtitle[1]{\newcommand\mrgsubtitle{#1}}
\newcommand\mrgproject{Torsten}
\newcommand\mrgtitle{Torsten: A Pharmacokinetic/Pharmacodynamic Model Library for Stan}
\newcommand\mrgsubtitle{\Large{User Manual} \linebreak (Torsten Version 0.84, Stan version 2.17.1)}
\include{mrgtitlepage}
\usepackage{imakeidx}
\makeindex
\usepackage[letterpaper, width=6.5in, height=9in]{geometry}
\usepackage{graphicx}
\usepackage{pdfpages}
\usepackage{amssymb}
\usepackage{epstopdf}
\usepackage{xcolor}
\definecolor{MRGGreen}{rgb}{0, 0.350, 0.200}
\usepackage[colorlinks=true, citecolor=MRGGreen, urlcolor=MRGGreen, linkcolor=MRGGreen]{hyperref}
\usepackage{courier}
\usepackage{listings}
\usepackage{siunitx}
\usepackage{booktabs}
\usepackage[framemethod=TikZ, skipabove=10pt, skipbelow=10pt, backgroundcolor=black!10, roundcorner=10pt, linewidth=1pt]{mdframed}
\BeforeBeginEnvironment{minted}{\begin{mdframed}}
\AfterEndEnvironment{minted}{\end{mdframed}}
\usepackage{subcaption}
\renewcommand{\chaptername}{}
\numberwithin{section}{chapter}
\theoremstyle{remark}
\newtheorem{example}{Example}
\newtheorem{remark}{Remark}


\usepackage[utf8]{inputenc}
\usepackage[T1]{fontenc}
\usepackage{graphicx}
\usepackage{grffile}
\usepackage{longtable}
\usepackage{wrapfig}
\usepackage{rotating}
\usepackage[normalem]{ulem}
\usepackage{amsmath}
\usepackage{textcomp}
\usepackage{amssymb}
\usepackage{capt-of}
\usepackage{hyperref}
\usepackage{minted}
\setcounter{secnumdepth}{2}
\author{Yi Zhang}
\date{\today}
\title{Torsten: A Pharmacokinetic/Pharmacodynamic Model Library for Stan\\\medskip
\large User Manual \\  (Torsten Version 0.84, Stan version 2.17.1)}
\hypersetup{
 pdfauthor={Yi Zhang},
 pdftitle={Torsten: A Pharmacokinetic/Pharmacodynamic Model Library for Stan},
 pdfkeywords={},
 pdfsubject={},
 pdfcreator={Emacs 25.3.1 (Org mode 9.1.3)}, 
 pdflang={English}}
\begin{document}

\maketitle
\tableofcontents


\section*{Development team}
\label{sec:org17a6f88}
\subsection*{Bill Gillespie}
\label{sec:orgd50f795}
\href{mailto:billg@metrumrg.com}{\texttt{billg@metrumrg.com}},
Metrum Research Group, LLC
\subsection*{Yi Zhang}
\label{sec:org63ec5f7}
\href{mailto:yiz@metrumrg.com}{\texttt{yiz@metrumrg.com}}, Metrum Research Group, LLC
\subsection*{Charles Margossian}
\label{sec:org875f835}
\href{mailto:charles.margossian@columbia.edu}{\texttt{charles.margossian@columbia.edu}}, Columbia University, Department of Statistics(formerly Metrum Research Group, LLC)

\chapter*{Acknowledgements}
\label{sec:orgf5f9ef5}
\section*{Institutions}
\label{sec:orgb44ca31}
We thank Metrum Research Group, Columbia University, and AstraZeneca.
\section*{Funding}
\label{sec:org40275d1}
This work was funded in part by the following organizations:
\subsection*{Office of Naval Research (ONR) contract N00014-16-P-2039}
\label{sec:orgd64c73d}
provided as part of the Small Business Technology Transfer (STTR)
program. The content of the information presented in this document
does not necessarily reflect the position or policy of the
Government and no official endorsement should be inferred.
\subsection*{Bill \& Melinda Gates Foundation.}
\label{sec:org4f260f5}
\section*{Individuals}
\label{sec:org6d04512}
We thank the Stan Development Team for giving us guidance on how to
create new Stan functions and adding features to Stan's core language
that facilitate building ODE-based models.

We also thank Kyle Baron and Hunter Ford for helpful advice on coding
in C++ and using GitHub, Curtis Johnston for reviewing the User
Manual, and Yaming Su for using Torsten and giving us feedback.

\chapter{Introduction}
\label{sec:org49a4bfe}
Stan is an open source probabilistic programing language designed
primarily to do Bayesian data analysis \cite{carpenter17_stan}. Several of its
features make it a powerful tool to specify and fit complex
models. Notably, its language is extremely flexible and its No U-Turn
Sampler (NUTS), an adaptative Hamiltonian Monte Carlo algorithm, has
proven more efficient than commonly used Monte Carlo Markov Chains
(MCMC) samplers for complex high dimensional problems \cite{hoffman_no-u-turn_2011}. Our
goal is to harness these innovative features and make Stan a better
software for pharmacometrics modeling. Our efforts are twofold:
\begin{enumerate}
\item We contribute to the development of new mathematical tools, such as functions that support differential equations based models, and implement them directly into Stan's core language.
\item We develop Torsten, an extension with specialized pharmacometrics functions.
\end{enumerate}

Throughout the process, we work very closely with the Stan Development
Team. We have benefited immensely from their mentorship, advice, and
feedback. Just like Stan, Torsten is an open source project that
fosters collaborative work. Interested in contributing? Shoot us an
e-mail and we will help you help us(\href{mailto:billg@metrumrg.com}{billg@metrumrg.com})!

Torsten is licensed under the BSD 3-clause license.

\begin{mdframed}
\textbf{WARNING:} The current version of Torsten is a \emph{prototype}. It
is being released for review and comment, and to support limited
research applications. It has not been rigorously tested and should
not be used for critical applications without further testing or
cross-checking by comparison with other methods.

We encourage interested users to try Torsten out and are happy to
assist. Please report issues, bugs, and feature requests on
\href{https://github.com/metrumresearchgroup/stan}{our GitHub page}.
\end{mdframed}

\section{Overview}
\label{sec:org195bc0f}
Torsten is a collection of Stan functions to facilitate analysis of
pharmacometric data using Stan. The current version
includes:
\begin{itemize}
\item Specific linear compartment models:
\begin{itemize}
\item One compartment model with first order absorption.
\item Two compartment model with elimination from and first order absorption into central compartment
\end{itemize}
\item General linear compartment model described by a system of first-order \underline{linear} Ordinary Differential Equations (ODEs).
\item General compartment model described by a system of first order ODEs.
\item Mix compartment model with PK forcing function described by a linear one or two compartment model.
\end{itemize}

The models and data format are based on
NONMEM\textregistered{}\footnote{NONMEM\textregistered{} is licensed and distributed by ICON Development Solutions.}/NMTRAN/PREDPP
conventions including:
\begin{itemize}
\item Recursive calculation of model predictions
\begin{itemize}
\item This permits piecewise constant covariate values
\end{itemize}
\item Bolus or constant rate inputs into any compartment
\item Handles single dose and multiple dose histories
\item Handles steady state dosing histories
\begin{itemize}
\item Note: The infusion time must be shorter than the inter-dose interval.
\end{itemize}
\item Implemented NMTRAN data items include: TIME, EVID, CMT, AMT, RATE, ADDL, II, SS
\end{itemize}

In general, all real variables may be passed as Stan parameters. A
few exceptions apply /to functions which use a numerical
integrator/(i.e. the general and the mix compartment
models). The below listed cases present technical difficulties, which we expect to
overcome in Torsten's next release:
\begin{itemize}
\item The RATE and TIME arguments must be fixed
\item In the case of a multiple truncated infusion rate dosing regimen:
\begin{itemize}
\item The bioavailability (F) and the amount (AMT) must be fixed.
\end{itemize}
\end{itemize}

This library provides Stan language functions that calculate amounts
in each compartment, given an event schedule and an ODE system.

\section{Installation}
\label{sec:orgeb4078b}
Installation files are available on GitHub

\begin{itemize}
\item \url{https://github.com/metrumresearchgroup/Torsten}
\end{itemize}

We are working with Stan development team to create a
system to add and share Stan packages. In the mean time,
the current repo contains forked version of Stan with
Torsten. The latest version of Torsten (v0.84) is
compatible with Stan v2.17.1. Torsten is agnostic to which
Stan interface you use. Here we provide command line and R
interfaces.

First, download the project. The root path of the project is
your \texttt{torsten\_path}. Set the envionment variable
\mintinline[breaklines=true,fontsize=\footnotesize,breakanywhere=true]{sh}{TORSTEN_PATH}
to \texttt{torsten\_path}. In bash this is 

\begin{minted}[breaklines=true,fontsize=\footnotesize,breakanywhere=true]{sh}
export TORSTEN_PATH=torsten_path
\end{minted}

\subsection*{Command line version}
\label{sec:orgf244a93}
There is no need to install command line version. To
build a Stan model with \texttt{model\_name} in \texttt{model\_path}
using command line, in \texttt{torsten\_path/cmdstan}, do
\begin{minted}[breaklines=true,fontsize=\footnotesize,breakanywhere=true]{sh}
make model_path/model_name
\end{minted}

\subsection*{R version}
\label{sec:org1dd4a96}
The R version is based on \href{https://cran.r-project.org/web/packages/rstan/index.html}{rstan}, the Stan's interface for
R. To install R version of Torsten, at \texttt{torsten\_path}, in R
\begin{minted}[breaklines=true,fontsize=\footnotesize,breakanywhere=true]{r}
source('install.R')
\end{minted}

Please ensure the R toolchain includes a C++ compiler with
C++11 support. In particular, R 3.3.0 and later is
recommended as it contains toolchain based on gcc 4.9.3. On
Windows platform, such a toolchain can be found in Rtools33 and later.

Please ensure CXXFLAGS in .R/Makevars constains flag
-std=c++11. 

For more information of installation troubleshooting,
please consult \href{https://github.com/stan-dev/rstan/wiki}{rstan wiki}.

\subsection*{Testing}
\label{sec:org4183109}
To test after installation, run
\begin{minted}[breaklines=true,fontsize=\footnotesize,breakanywhere=true]{sh}
./test-torsten.sh --unit --signature --model
\end{minted}
\section{Implementation details}
\label{sec:org2883d82}
\begin{itemize}
\item Stan version 2.17.1
\item All functions are programmed in C++ and are compatible
with the Stan math automatic differentiation library \cite{carpenter15_stan_math_librar}
\item One and two compartment models: hand-coded analytical solutions
\item General linear compartment models with semi-analytical solutions using the built-in matrix exponential function
\item General compartment models with numerical solutions using built-in ODE integrators in Stan. The tuning parameters of the solver are adjustable. The steady state solution is calculated using a numerical algebraic solver.
\item Mix compartment model: the PK forcing function is solved
analytically and the forced ODE system is solved
numerically.
\end{itemize}

\section{Development plans}
\label{sec:orgced1241}
Our current plans for future development of Torsten include the
following:
\begin{itemize}
\item Build a system to easily share packages of Stan functions
(written in C++ or in the Stan language)
\item Allow numerical methods to handle RATE, AMT, TIME, and the
bioavailability fraction (F) as parameters in all cases.
\item Optimize Matrix exponential functions
\begin{itemize}
\item Function for the action of Matrix Exponential on a vector
\item Hand-coded gradients
\item Special algorithm for matrices with special properties
\end{itemize}
\item Fix issue that arises when computing the adjoint of the lag time
parameter (in a dosing compartment) evaluated at \(t_{\text{lag}} = 0\).
\item Extend formal tests
\begin{itemize}
\item We want more C++ Google unit tests to address cases users may
encounter
\item Comparison with simulations from the R package
\emph{mrgsolve} and the software NONMEM\textregistered{}
\item Recruit non-developer users to conduct beta testing
\end{itemize}
\end{itemize}

\section{Changelog}
\label{sec:org6cb8734}
\subsection*{0.84 \textit{<2018-02-24 Sat>}}
\label{sec:org528a125}
\begin{itemize}
\item Added
\label{sec:orgd746f42}
\begin{itemize}
\item Piecewise linear interpolation function.
\item Univariate integral functions.
\end{itemize}

\item Changed
\label{sec:orge72f6a9}
\begin{itemize}
\item Update with Stan version 2.17.1.
\item Minor revisions to User Manual.
\item Bugfixes.
\end{itemize}
\end{itemize}

\subsection*{0.83 \textit{<2017-08-02 Wed>}}
\label{sec:orge30c9ba}
\begin{itemize}
\item Added
\label{sec:orgd33cec0}
\begin{itemize}
\item Work with TorstenHeaders
\item Each chain has a different initial estimate
\end{itemize}

\item Changed
\label{sec:orgc9dc5b4}
\begin{itemize}
\item User manual
\item Fix misspecification in ODE system for TwoCpt example.
\item Other bugfixes
\end{itemize}
\end{itemize}


\subsection*{0.82 \textit{<2017-01-29 Sun>}}
\label{sec:orgc2f55bc}
\begin{itemize}
\item Added
\label{sec:orgc8a44cb}
\begin{itemize}
\item Allow parameter arguments to be passed as 1D or 2D arrays
\item More unit tests
\item Unit tests check automatic differentiation against finite differentiation.
\end{itemize}

\item Changed
\label{sec:org05b6a3e}
\begin{itemize}
\item Split the parameter argument into three arguments: pMatrix
(parameters for the ODEs -- note: for linOdeModel, pMatrix
is replaced by the constant rate matrix K), biovar
(parameters for the biovariability), and tlag (parameters
for the lag time).
\item bugfixes
\end{itemize}
\end{itemize}

\subsection*{0.81 \textit{<2016-09-27 Tue>}}
\label{sec:orgd5c003e}
\begin{itemize}
\item Added
\label{sec:org4d1864e}
linCptModel (linear compartmental model) function
\end{itemize}

\subsection*{0.80a \textit{<2016-09-21 Wed>}}
\label{sec:org2469271}
\begin{itemize}
\item Added
\label{sec:org7435c7c}
check\(_{\text{finite}}\) statements in pred\(_{\text{1}}\) and pred\(_{\text{2}}\) to reject metropolis proposal if initial conditions are not finite
\end{itemize}




\chapter{Using Torsten}
\label{sec:org3918ecd}
The reader should have a basic understanding of how Stan works before
reading this chapter. There are excellent resources online to get
started with Stan

\begin{itemize}
\item \href{http://mc-stan.org/documentation}{http://mc-stan.org/documentation}
\end{itemize}


In this section we go through the different functions Torsten adds to
Stan. It will be helpful to apply these functions to a simple
example. We have uploaded code and data on

\begin{itemize}
\item \href{https://github.com/metrumresearchgroup/example-models}{https://github.com/metrumresearchgroup/example-models}
\end{itemize}

We use the following example to demonstrate use of several functions in the Torsten library.
\subsection*{Two compartment model}
\label{sec:orgf21ab7b}
We model drug absorption in a single patient and simulate plasma drug concentrations:

\begin{itemize}
\item Multiple Doses: 1250 mg, every 12 hours, for a total of 15 doses
\item PK: plasma concentrations of parent drug (\(c\))
\item PK measured at 0.083, 0.167, 0.25, 0.5, 0.75, 1, 1.5, 2, 4, 6,
8, 10 and 12 hours after 1st, 2nd, and 15th dose. In addition, the
PK is measured every 12 hours throughout the trial.
\end{itemize}

The plasma concentration (\(c\)) are simulated according to the following equations:
\begin{eqnarray*}
  \log\left(c\right) &\sim& N\left(\log\left(\widehat{c}\right),\sigma^2\right) \\
  \widehat{c} &=& f_{2cpt}\left(t, CL, Q, V_2, V_3, k_a\right) \\
  \left(CL, Q, V_2, V_3, ka\right) &=& 
                                       \left(5\ {\rm L/h}, 8\  {\rm L/h}, 20\  {\rm L},  70\ {\rm L}, 1.2\ {\rm h^{-1}} \right) \\
  \sigma^2 &=& 0.01
\end{eqnarray*}
and the drug concentration is given by \(c = y_2/V_2\).

where the mass of drug in the central compartment (\(y_2\)) is obtained
by solving the system of ordinary differential equations (ODEs):
\begin{align*}
  y_1' &= -k_a y_1 \\
  y_2' &= k_a y_1 - \left(\frac{CL}{V_2} + \frac{Q}{V_2}\right) y_2 +  \frac{Q}{V_3}  y_3  \\ 
  y_3' &= \frac{Q}{V_2} y_2 - \frac{Q}{V_3} y_3
\end{align*}

The data are generated using the R package \texttt{mrgsolve} \cite{Baron000}. 


\section{One Compartment Model}
\label{sec:org708bdeb}
\index{One Compartment Model}
\begin{minted}[breaklines=true,fontsize=\footnotesize,breakanywhere=true]{stan}
real[] = PKModelOneCpt(real[] time, real[] amt, real[] ate,
                       real[] ii, int[] evid, int[] cmt,
                       real[] addl, int[] ss, real[] theta,
                       real[] biovar, real[] tlag)
\end{minted}

Parameters in \texttt{theta} : \(CL\), \(V_2\), \(k_a\).

The event arguments. \texttt{time}, \texttt{amt}, \texttt{rate}, \texttt{ii}, \texttt{evid}, \texttt{cmt}, \texttt{addl}, and
\texttt{ss}, describe the event schedule of the clinical
trial. All arrays have the same length, which
corresponds to the number of events.

The model arguments, \texttt{theta} contains the ODE
parameters, \texttt{biovar} the bioavailability fraction in each
compartment, and \texttt{tlag} the
lag time in each compartment. The model arguments may be either one
or two dimensional arrays. If they are one dimensional arrays, the
parameters are constant for all events. If they are two dimensional
arrays then each row contains the parameters for the interval
\texttt{[ time[i-1], time[i] ]}. The number of rows should equal the
number of events.

Setting \(ka\) to 0 eliminates the first-order absorption.

\begin{figure}[htbp]
\centering
\includegraphics[width=2.0in]{./graphics/cptModels.png}
\caption{\label{fig:orge46f6ee}
One and two compartment models with first order absorption implemented in Torsten.}
\end{figure}

\section{Two Compartment Model}
\label{sec:org442e92f}
\index{Two Compartment Model}
\begin{minted}[breaklines=true,fontsize=\footnotesize,breakanywhere=true]{stan}
real[] = PKModelTwoCpt(real[] time, real[] amt, real[] ate,
                       real[] ii, int[] evid, int[] cmt,
                       real[] addl, int[] ss, real[] theta,
                       real[] biovar, real[] tlag)
\end{minted}

Parameters in \texttt{theta} : \(CL\), \(Q\), \(V_2\), \(V_3\), \(k_a\)

See section \ref{org8870e8d} for function description.

Code example below shows Stan example of using
\texttt{TwoCptModel} to fit \hyperref[sec:orgf21ab7b]{Two compartment model}. Three MCMC chains of 2000 iterations were simulated. The first
1000 iteration of each chain were discarded. Thus 1000 MCMC samples
per chain were used for the subsequent analyses.

\begin{minted}[breaklines=true,fontsize=\footnotesize,breakanywhere=true]{stan}
data{
  int<lower = 1> nt;  // number of events
  int<lower = 1> nObs;  // number of observation
  int<lower = 1> iObs[nObs];  // index of observation

  // NONMEM data
  int<lower = 1> cmt[nt];
  int evid[nt];
  int addl[nt];
  int ss[nt];
  real amt[nt];
  real time[nt];
  real rate[nt];
  real ii[nt];

  vector<lower = 0>[nObs] cObs;  // observed concentration (Dependent Variable)
}

transformed data{
  vector[nObs] logCObs = log(cObs);
  int nTheta = 5;  // number of ODE parameters in Two Compartment Model
  int nCmt = 3;  // number of compartments in model

  // Since we're not trying to evaluate the bio-variability (F) and 
  // the lag times, we declare them as data.
  real biovar[nCmt];
  real tlag[nCmt];

  biovar[1] = 1;
  biovar[2] = 1;
  biovar[3] = 1;

  tlag[1] = 0;
  tlag[2] = 0;
  tlag[3] = 0;

}

parameters{
  real<lower = 0> CL;
  real<lower = 0> Q;
  real<lower = 0> V1;
  real<lower = 0> V2;
  real<lower = 0> ka;
  real<lower = 0> sigma;

}

transformed parameters{
  real theta[nTheta];  // ODE parameters
  vector<lower = 0>[nt] cHat;
  vector<lower = 0>[nObs] cHatObs;
  matrix<lower = 0>[nt, nCmt] x; 

  theta[1] = CL;
  theta[2] = Q;
  theta[3] = V1;
  theta[4] = V2;
  theta[5] = ka;

  // PKModelTwoCpt takes in the NONMEM data, followed by the parameter
  // arrays abd returns a matrix with the predicted amount in each 
  // compartment at each event.
  x = PKModelTwoCpt(time, amt, rate, ii, evid, cmt, addl, ss,
                   theta, biovar, tlag);

  cHat = col(x, 2) ./ V1; // we're interested in the amount in the second compartment 

  cHatObs = cHat[iObs]; // predictions for observed data recors
\end{minted}

The MCMC history plots(Figure \ref{fig:org44eb541})
suggest that the 3 chains have converged to common distributions for
all of the key model parameters. The fit to the plasma concentration
data (Figure \ref{fig:org378ba74}) are in close agreement with the
data, which is not surprising since the fitted model is identical to
the one used to simulate the data. Similarly the parameter estimates
summarized in Table \ref{tab:orgc99abfe} and Figure \ref{fig:orgecaa462}
are consistent with the values used for simulation.

\begin{table}[htbp]
\caption{\label{tab:orgc99abfe}
Summary of the MCMC simulations of the marginal posterior distribu- tions of the model parameters}
\centering
\begin{tabular}{lrrrrrrrrrr}
\hline
 & mean & se\(_{\text{mean}}\) & sd & 2.5\% & 25\% & 50\% & 75\% & 97.5\% & n\(_{\text{eff}}\) & Rhat\\
\hline
CL & 4.823 & 0.002 & 0.092 & 4.647 & 4.762 & 4.823 & 4.883 & 5.012 & 2392.155 & 1.00\\
Q & 7.596 & 0.013 & 0.586 & 6.479 & 7.201 & 7.594 & 7.977 & 8.785 & 1923.939 & 1.00\\
V1 & 21.073 & 0.069 & 2.573 & 16.017 & 19.352 & 21.046 & 22.817 & 26.097 & 1385.883 & 1.00\\
V2 & 76.365 & 0.105 & 5.611 & 65.805 & 72.623 & 76.172 & 79.916 & 87.971 & 2862.184 & 1.00\\
ka & 1.231 & 0.004 & 0.177 & 0.907 & 1.107 & 1.221 & 1.344 & 1.599 & 1581.825 & 1.00\\
sigma & 0.109 & 0.000 & 0.012 & 0.089 & 0.100 & 0.108 & 0.116 & 0.134 & 2560.112 & 1.00\\
\hline
\end{tabular}
\end{table}


\begin{figure}[htbp]
\centering
\includegraphics[width=0.6\linewidth]{../example-models/R/deliv/figure/TwoCptModel/TwoCptModelPlots001.pdf}
\caption{\label{fig:org44eb541}
MCMC history plots for the parameters of a two compartment model with first order absorption (each color corresponds to a different chain)}
\end{figure}

\begin{figure}[htbp]
\centering
\includegraphics[width=0.6\linewidth]{../example-models/R/deliv/figure/TwoCptModel/TwoCptModelPlots002.pdf}
\caption{\label{fig:org3039ae9}
MCMC history plots for the parameters of a two compartment model with first order absorption (each color corresponds to a different chain)}
\end{figure}

\begin{figure}[htbp]
\centering
\includegraphics[width=0.6\linewidth]{../example-models/R/deliv/figure/TwoCptModel/TwoCptModelPlots003.pdf}
\caption{\label{fig:orgecaa462}
Posterior Marginal Densities of the Model Parameters of a two compartment model with first order absorption (each color corresponds to a different chain)}
\end{figure}

\begin{figure}[htbp]
\centering
\includegraphics[width=0.6\linewidth]{../example-models/R/deliv/figure/TwoCptModel/TwoCptModelPlots006.pdf}
\caption{\label{fig:org378ba74}
Predicted (posterior median and 90\% credible intervals) and observed plasma drug concentrations of a two compartment model with first order absorption}
\end{figure}


\section{General Linear ODE Model Function}
\label{sec:org72f0d28}
\index{General linear model}
\begin{minted}[breaklines=true,fontsize=\footnotesize,breakanywhere=true]{stan}
real[] = linOdeModel(real[] time, real[] amt, real[] rate,
                     real[] ii, int[] evid, int[] cmt,
                     real[] addl, int[] ss,
                     matrix K, real[] biovar, real[] tlag)
\end{minted}
A general linear ODE model refers to a model that may be described in
terms of a system of first order linear differential equations with
(piecewise) constant coefficients, i.e., a differential equation of
the form:
\begin{equation}
y^\prime\left(t\right) = Ky\left(t\right)
\end{equation}
where \(K\) is a matrix. For example \(K\) for a two compartment model
(equation \eqref{eq:TwoCpt}) with first order absorption is:
\begin{equation}
  K = \left[\begin{array}{ccc}
              -k_a & 0 & 0 \\
              k_a & -\left(k_{10} + k_{12}\right) & k_{21} \\
              0 & k_{12} & -k_{21}
            \end{array}\right]
\end{equation}
where \(k_{10}=CL/V_2\), \(k_{12}=Q/V_2\), and \(k_{21}=Q/V_3\).

System parameters is in \texttt{K}, with \texttt{K} being the constant rate
matrix is the same for all events or an array of constant rate matrices. The length of the array is
the number of events and each element corresponds to the matrix at
the interval \texttt{[ time[i-1], time[i] ]}.
Note that \texttt{K} contains all the ODE parameters, so we no
longer need \texttt{theta}.

The following Stan example illustrate
the use of General linear model for fitting a two compartment model
with first order absorption.

\begin{minted}[breaklines=true,fontsize=\footnotesize,breakanywhere=true]{stan}
// LinTwoCptModelExample.stan
// Run two compartment model using matrix exponential solution
// Heavily anotated to help new users

data{
  int<lower = 1> nt;  // number of events
  int<lower = 1> nObs;  // number of observations
  int<lower = 1> iObs[nObs];  // index of observation

  // NONMEM data
  int<lower = 1> cmt[nt];
  int evid[nt];
  int addl[nt];
  int ss[nt];
  real amt[nt];
  real time[nt];
  real rate[nt];
  real ii[nt];

  vector<lower = 0>[nObs] cObs;  // observed concentration (dependent variable)
}

transformed data{
  vector[nObs] logCObs = log(cObs);
  int nCmt = 3;
  real biovar[nCmt];
  real tlag[nCmt];

  for (i in 1:nCmt) {
    biovar[i] = 1;
    tlag[i] = 0;
  }
}

parameters{
  real<lower = 0> CL;
  real<lower = 0> Q;
  real<lower = 0> V1;
  real<lower = 0> V2;
  real<lower = 0> ka;
  real<lower = 0> sigma;

}

transformed parameters{
  matrix[3, 3] K;
  real k10 = CL / V1;
  real k12 = Q / V1;
  real k21 = Q / V2;
  vector<lower = 0>[nt] cHat;
  vector<lower = 0>[nObs] cHatObs;
  matrix<lower = 0>[nt, 3] x;

  K = rep_matrix(0, 3, 3);

  K[1, 1] = -ka;
  K[2, 1] = ka;
  K[2, 2] = -(k10 + k12);
  K[2, 3] = k21;
  K[3, 2] = k12;
  K[3, 3] = -k21;

  // linModel takes in the constant rate matrix, the object theta which
  // contains the biovariability fraction and the lag time of each compartment,
  // and the NONMEM data.
  x = linOdeModel(time, amt, rate, ii, evid, cmt, addl, ss,
                  K, biovar, tlag);
\end{minted}

\section{General ODE Model Function}
\label{sec:orgeb652d6}
\index{General ODE Model}
\begin{minted}[breaklines=true,fontsize=\footnotesize,breakanywhere=true]{stan}
real[] model_name(ODE_system, int nCmt,
                  real[] time, real[] amt, real[] rate, real[] ii,
                  int[] evid, int[] cmt, real[] addl, int[] ss,
                  real[] theta, real[] biovar, real[] tlag,                      
                  real rel_tol, real abs_tol, int max_step)
\end{minted}

Torsten may be used to fit models described by a system of
user-specified first-order ODEs, i.e., differential equations of the form:
\begin{equation*}
y'(t) = f(t, y(t))
\end{equation*}
In the case where the rate vector \(R\) is non-zero, this equation becomes:
\begin{equation*}
y'(t) = f(t, y(t)) + R
\end{equation*}

\texttt{ODE\_system} specifies \(f(t, y(t))\), which the user
defines inside the \texttt{functions} block (see section 19.2 of the
Stan reference manual for details and Figure\textasciitilde{}\ref{GenTwoCptModelCode}
for an example). The user does NOT include the rates in their
definition of \(f\). Torsten automatically corrects the derivatives when
the rates are non-zero.

\texttt{nCmt} is the number of compartments (or, equivalently, the
number of ODEs) in the model. \texttt{rel\_tol}, \texttt{abs\_tol},
and \texttt{max\_step} are tuning parameters for the ODE integrator:
respectively the relative tolerance, the absolute tolerance, and the
maximum number of steps.

The options for \texttt{model\_name} are:
\begin{itemize}
\item \texttt{generalOdeModel\_rk45}
\item \texttt{generalOdeModel\_bdf}
\end{itemize}

They respectively call the built-in Runge-Kutta 4th/5th order (rk45)
integrator, recommended for non-stiff ODEs, and the Backward
Differentiation (BDF) integrator, recommended for stiff ODEs. Which
value to use for the tuning parameters depends on the integrator and
the specifics of the ODE system. Reducing the tolerance parameters and
increasing the number of steps make for a more robust integrator but
can significantly slow down the algorithm. The following can be used
as a starting point: 
\begin{itemize}
\item \texttt{rel\_tol = 1e-6}
\item \texttt{abs\_tol = 1e-6}
\item \texttt{max\_step = 1e+6}
\end{itemize}
for rk45 integrator and
\begin{itemize}
\item \texttt{rel\_tol = 1e-10}
\item \texttt{abs\_tol = 1e-10}
\item \texttt{max\_step = 1e+8}
\end{itemize}
for the bdf integrator \footnote{These are the default tuning parameters the integrators. Torsten functions do not have a default values for these parameters. The user must explicitly pass the tuning parameters to \texttt{generalOdeModel\_*()} .}.
Users should be prepared to adjust these
values. For additional information, see the Stan User's
Manual \cite{stan_team_2017}.

A few notable restrictions apply to :
\begin{itemize}
\item \texttt{rate} and \texttt{time} cannot be passed as parameters.
\item In the case of a multiple truncated infusion rate dosing regimen:
\begin{itemize}
\item The bioavailability \texttt{biovar}) and the amount \texttt{amt}) cannot be passed as parameters.
\end{itemize}
\end{itemize}
These restrictions also apply to \texttt{mixOdeCpt\_*} functions, discussed in the next section.

\begin{minted}[breaklines=true,fontsize=\footnotesize,breakanywhere=true]{stan}
// GenTwoCptModelExample.stan
// Run two compartment model using numerical solution
// Heavily anotated to help new users

functions{

  // define ODE system for two compartmnt model
  real[] twoCptModelODE(real t,
                        real[] x,
                        real[] parms,
                        real[] rate,  // in this example, rate is treated as data
                        int[] dummy){

    // Parameters
    real CL = parms[1];
    real Q = parms[2];
    real V1 = parms[3];
    real V2 = parms[4];
    real ka = parms[5];

    // Re-parametrization
    real k10 = CL / V1;
    real k12 = Q / V1;
    real k21 = Q / V2;

    // Return object (derivative)
    real y[3];  // 1 element per compartment of
                // the model

    // PK component of the ODE system
    y[1] = -ka*x[1];
    y[2] = ka*x[1] - (k10 + k12)*x[2] + k21*x[3];
    y[3] = k12*x[2] - k21*x[3];

    return y;
  }

}
data{
  int<lower = 1> nt;  // number of events
  int<lower = 1> nObs;  // number of observations
  int<lower = 1> iObs[nObs];  // index of observation

  // NONMEM data
  int<lower = 1> cmt[nt];
  int evid[nt];
  int addl[nt];
  int ss[nt];
  real amt[nt];
  real time[nt];
  real rate[nt];
  real ii[nt];

  vector<lower = 0>[nObs] cObs;  // observed concentration (dependent variable)
}

transformed data{
  vector[nObs] logCObs = log(cObs);
  int nTheta = 5;   // number of parameters
  int nCmt = 3;   // number of compartments
  real biovar[nCmt];
  real tlag[nCmt];

  for (i in 1:nCmt) {
    biovar[i] = 1;
    tlag[i] = 0;
  }
}

parameters{
  real<lower = 0> CL;
  real<lower = 0> Q;
  real<lower = 0> V1;
  real<lower = 0> V2;
  real<lower = 0> ka;
  real<lower = 0> sigma;
}

transformed parameters{
  real theta[nTheta];
  vector<lower = 0>[nt] cHat;
  vector<lower = 0>[nObs] cHatObs;
  matrix<lower = 0>[nt, 3] x; 

  theta[1] = CL;
  theta[2] = Q;
  theta[3] = V1;
  theta[4] = V2;
  theta[5] = ka;

  // generalCptModel takes in the ODE system, the number of compartment 
  // (here we have a two compartment model with first order absorption, so
  // three compartments), the parameters matrix, the NONEM data, and the tuning
  // parameters (relative tolerance, absolute tolerance, and maximum number of steps)
  // of the ODE integrator. The user can choose between the bdf and the rk45 integrator.
  // Returns a matrix with the predicted amount in each compartment 
  // at each event.

//  x = generalOdeModel_bdf(twoCptModelODE, 3,
//                          time, amt, rate, ii, evid, cmt, addl, ss,
//                          theta, biovar, tlag,
//                          1e-8, 1e-8, 1e8);

   x = generalOdeModel_rk45(twoCptModelODE, 3,
                           time, amt, rate, ii, evid, cmt, addl, ss,
                           theta, biovar, tlag,
                           1e-6, 1e-6, 1e6);

  cHat = col(x, 2) ./ V1;

  for(i in 1:nObs){
    cHatObs[i] = cHat[iObs[i]];  // predictions for observed data records
  }
}
\end{minted}

\section{Mixed ODE Model Function}
\label{sec:org2f7aebe}
\index{Mixed ODE Model}

\begin{minted}[breaklines=true,fontsize=\footnotesize,breakanywhere=true]{stan}
real[] model_name(reduced_ODE_system, int nOde,
                  real[] time, real[] amt, real[] rate,
                  real[] ii, int[] evid, int[] cmt, real[]
                  addl, int[] ss,
                  real[] theta, real[] biovar, real[] tlag,
                  real rel_tol, real abs_tol, real max_step)
\end{minted}

In certain cases, an ODE system can be divided in two subsystems:

\begin{align*}
  y_1^\prime &= f_1(t, y_1) \\
  y_2^\prime &= f_2(t, y_1, y_2)
\end{align*}
where \(y_1\), \(y_2\), \(f_1\), and \(f_2\) are vector-valued functions, and
\(y_1^\prime\) is independent of \(y_2\). This structure arises in PK/PD
models, where \(y_1\) describes a forcing PK function and \(y_2\) the PD
effects. If \(y_1\) has an analytical solution, we can construct a
\textit{mixed solver}, which analytically solves \(y_1\) and numerically
integrates \(y_2\). This approach leads to an appreciable gain in
computational efficiency. In the example of a Friberg-Karlsson
semi-mechanistic model, we observe an average speedup of
\(\sim 47 \pm 18 \%\) when using the mix solver in lieu of the numerical
integrator. Torsten supports the mixed solver for
cases where \(y_1\) solves the ODEs for a One or Two Compartment model
with a first-order absorption.

The \texttt{reduced\_ODE\_system} specifies the system we
numerically solve (\(y_2\) in the above discussion, also called the
\emph{reduced system} and \texttt{nOde} the number of equations in
the \underline{reduced} system. The function that defines a reduced
system has an almost identical signature to that used for a full
system, but takes one additional argument: \(y_1\), the PKstates,
i.e. solution to the PK ODEs.
\begin{minted}[breaklines=true,fontsize=\footnotesize,breakanywhere=true]{stan}
real[] reducedODE(real t,  // time
                  real[] y,  // reduced state
                  real[] y1,  // PK states}'
                  real[] theta,  // parameters
                  real[] x_r,  // data (real)
                  int[] x_int)  // data (integer)
\end{minted}

The options for \texttt{modelName} are:
\begin{itemize}
\item \texttt{mixOde1CptModel\_rk45}
\item \texttt{mixOde1CptModel\_bdf}
\item \texttt{mixOde2CptModel\_rk45}
\item \texttt{mixOde2CptModel\_bdf}
\end{itemize}

These four functions correspond to all the permutations we can obtain
when using a forcing One or Two Compartment function, and the
Runge-Kutta 4th/5th order (rk45) or Backward Differentiation (BDF)
integration method. The mixed ODE functions can be used to compute the
steady state solutions supported by the general ODE model functions.

Restrictions regarding which arguments may be passed as parameters for
\texttt{generalOdeModel\_*} also apply to
\texttt{mixOdeCptModel\_*}.

We cannot apply the mixed solver to the Two Compartment example we
have been using so far. Instead, we will consider the model which
motivated the implementation of the method in the first place.

\section{Friberg-Karlsson Semi-Mechanistic Model}
\label{sec:org6cf1968}
\index{Friberg-Karlsson Model}
In this second example, we add to our two Compartment model a PD
effect, described by a system of nonlinear ODEs \cite{friberg_mechanistic_2003}.
\subsection*{Friberg-Karlsson Model}
\label{sec:orgb118848}
Neutropenia is observed in patients receiving an ME-2 drug. Our goal
is to model the relation between neutrophil counts and drug
exposure. Using a feedback mechanism, the body maintains the number of
neutrophils at a baseline value (Figure\textasciitilde{}\ref{FK}). While in the
patient's blood, the drug impedes the production of neutrophils. As a
result, the neutrophil count goes down. After the drug clears out, the
feedback mechanism kicks in and brings the neutrophil count back to
baseline.

\begin{align}
  \log(ANC_i) \sim& N(\log(Circ), \sigma^2_{ANC})  \\
  Circ =& f_{FK}(MTT, Circ_{0}, \alpha, \gamma, c)  \\
  (MTT, Circ_{0}, \alpha, \gamma, ktr) =& (125, 5.0, 3 \times 10^{-4}, 0.17) \\
  \sigma^2_{ANC} =& 0.001
\end{align}
where \(c\) is the drug concentration in the blood we get from the Two
Compartment model, and \(Circ\) is obtained by solving the following
system of nonlinear ODEs:

\begin{eqnarray}
  \begin{aligned}
   y_\mathrm{prol}' &= k_\mathrm{prol} y_\mathrm{prol} (1 - E_\mathrm{drug})\left(\frac{Circ_0}{y_\mathrm{circ}}\right)^\gamma - k_\mathrm{tr}y_\mathrm{prol} \\
   y_\mathrm{trans1}' &= k_\mathrm{tr} y_\mathrm{prol} - k_\mathrm{tr} y_\mathrm{trans1} \\
   y_\mathrm{trans2}' &= k_\mathrm{tr} y_\mathrm{trans1} - k_\mathrm{tr} y_\mathrm{trans2}  \\
   y_\mathrm{trans3}' &= k_\mathrm{tr} y_\mathrm{trans2} - k_\mathrm{tr} y_\mathrm{trans3}  \\
   y_\mathrm{circ}' &= k_\mathrm{tr} y_\mathrm{trans3} - k_\mathrm{tr} y_\mathrm{circ}
   \end{aligned}
   \label{eq:FK}
\end{eqnarray}
where \(E_{drug}  = \alpha c\).

The ODEs specifying the Two Compartment Model
(equation\textasciitilde{}\ref{eq:TwoCpt}) do not depend on the PD ODEs
(equation\textasciitilde{}\ref{eq:FK}) and can be solved analytically by Torsten. We
therefore specify our model using a mixed solver function. We do not
expect our system to be stiff and use the Runge-Kutta 4th/5th order
integrator (Figures \ref{FK_reduced} and \ref{FK_mix2}).

\begin{minted}[breaklines=true,fontsize=\footnotesize,breakanywhere=true]{stan}
functions{
  real[] FK_ODE(real t,
                real[] y,
                real[] y_pk,
                real[] theta,
                real[] rdummy,
                int[] idummy){
    /* PK variables */
    real VC = theta[3];

    /* PD variable */
    real mtt      = theta[6];
    real circ0    = theta[7];
    real alpha    = theta[8];
    real gamma    = theta[9];
    real ktr      = 4.0 / mtt;
    real prol     = y[1] + circ0;
    real transit1 = y[2] + circ0;
    real transit2 = y[3] + circ0;
    real transit3 = y[4] + circ0;
    real circ     = fmax(machine_precision(), y[5] + circ0);
    real conc     = y_pk[2] / VC;
    real EDrug    = alpha * conc;

    real dydt[5];

    dydt[1] = ktr * prol * ((1 - EDrug) * ((circ0 / circ)^gamma) - 1);
    dydt[2] = ktr * (prol - transit1);
    dydt[3] = ktr * (transit1 - transit2);
    dydt[4] = ktr * (transit2 - transit3);
    dydt[5] = ktr * (transit3 - circ);

    return dydt;
  }
}

data{
  int<lower = 1> nt;
  int<lower = 1> nObsPK;
  int<lower = 1> nObsPD;
  int<lower = 1> iObsPK[nObsPK];
  int<lower = 1> iObsPD[nObsPD];
  real<lower = 0> amt[nt];
  int<lower = 1> cmt[nt];
  int<lower = 0> evid[nt];
  real<lower = 0> time[nt];
  real<lower = 0> ii[nt];
  int<lower = 0> addl[nt];
  int<lower = 0> ss[nt];
  real rate[nt];
  vector<lower = 0>[nObsPK] cObs;
  vector<lower = 0>[nObsPD] neutObs;

  real<lower = 0> circ0Prior;
  real<lower = 0> circ0PriorCV;
  real<lower = 0> mttPrior;
  real<lower = 0> mttPriorCV;
  real<lower = 0> gammaPrior;
  real<lower = 0> gammaPriorCV;
  real<lower = 0> alphaPrior;
  real<lower = 0> alphaPriorCV;
}

transformed data{
  int nOde = 5;
  vector[nObsPK] logCObs;
  vector[nObsPD] logNeutObs;
//  int idummy[0];
//  real rdummy[0];

  int nTheta;
  int nIIV;

  int n;                        /* ODE dimension */
  real rtol;
  real atol;
  int max_step;

  n = 8;
  rtol = 1e-8;
  atol = 1e-8;
  max_step = 100000;

  logCObs = log(cObs);
  logNeutObs = log(neutObs);

  nIIV = 7; // parameters with IIV
  nTheta = 9; // number of parameters
}

parameters{

  real<lower = 0> CL;
  real<lower = 0> Q;
  real<lower = 0> VC;
  real<lower = 0> VP;
  real<lower = 0> ka;
  real<lower = 0> mtt;
  real<lower = 0> circ0;
  real<lower = 0> alpha;
  real<lower = 0> gamma;
  real<lower = 0> sigma;
  real<lower = 0> sigmaNeut;

  // IIV parameters
  cholesky_factor_corr[nIIV] L;
  vector<lower = 0>[nIIV] omega;
}

transformed parameters{
  vector[nt] cHat;
  vector<lower = 0>[nObsPK] cHatObs;
  vector[nt] neutHat;
  vector<lower = 0>[nObsPD] neutHatObs;
  real<lower = 0> theta[nTheta];
  matrix[nt, nOde + 3] x;
  real biovar[nTheta];
  real tlag[nTheta];

  for (i in 1:nTheta) {
    biovar[i] = 1.0;
    tlag[i] = 0.0;
  }

  theta[1] = CL;
  theta[2] = Q;
  theta[3] = VC;
  theta[4] = VP;
  theta[5] = ka;
  theta[6] = mtt;
  theta[7] = circ0;
  theta[8] = alpha;
  theta[9] = gamma;

  x = mixOde2CptModel_rk45(FK_ODE, nOde, time, amt, rate, ii, evid, cmt, addl, ss, theta, biovar, tlag, rtol, atol, max_step);

  cHat = col(x, 2) / VC;
  neutHat = col(x, 8) + circ0;

  for(i in 1:nObsPK) cHatObs[i]    = cHat[iObsPK[i]];
  for(i in 1:nObsPD) neutHatObs[i] = neutHat[iObsPD[i]];

}

model {

  // Priors
  CL    ~ normal(0, 20);
  Q     ~ normal(0, 20);
  VC    ~ normal(0, 100);
  VP    ~ normal(0, 1000);
  ka    ~ normal(0, 5);
  sigma ~ cauchy(0, 1);

  mtt       ~ lognormal(log(mttPrior), mttPriorCV);
  circ0     ~ lognormal(log(circ0Prior), circ0PriorCV);
  alpha     ~ lognormal(log(alphaPrior), alphaPriorCV);
  gamma     ~ lognormal(log(gammaPrior), gammaPriorCV);
  sigmaNeut ~ cauchy(0, 1);

  // Parameters for Matt's trick
  L ~ lkj_corr_cholesky(1);
  omega ~ cauchy(0, 1);

  // observed data likelihood
  logCObs ~ normal(log(cObs), sigma);
  logNeutObs ~ normal(log(neutObs), sigmaNeut);
}
\end{minted}

\appendix
\printindex
\backmatter

\bibliography{torsten}
\bibliographystyle{plain}
\end{document}
